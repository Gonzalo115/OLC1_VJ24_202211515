package analisis;

//importaciones
import java_cup.runtime.Symbol;
import java.util.LinkedList;
import abstracto.Instruccion;
import simbolo.Tipo;
import simbolo.tipoDato;

//instrucciones
import instrucciones.Print;
import instrucciones.Declaracion;
import instrucciones.AsignacionVC;
import instrucciones.Incremento;
import instrucciones.Decremento;

//expresiones
import expresiones.Nativo;
import expresiones.Aritmeticas;
import expresiones.OperadoresAritmeticos;
import expresiones.Relacionales;
import expresiones.OperadoresRelacionales;
import expresiones.Logicos;
import expresiones.OperadoresLogicos;
import expresiones.AccesoVC;


// parser
parser code
{:
    scanner s;
    parser(scanner s){this.s = s;}
    
    public void syntax_error(Symbol s){
        System.out.println("Error Sintactico en la linea " +
        (s.left) + " y columna " + (s.right) +
        ". No se esperaba el componente: " + (s.value) + ".");
    }

    public void unrecovered_syntax_error (Symbol s){
        System.out.println("Error Sintactico no recuperable en la linea" +
        (s.left) + " y columna " + (s.right) +
        ". No se esperaba el componente: " + (s.value) + ".");
    }

:}

action code
{:
:}

//terminales
terminal String CADENA, CARACTER, ENTERO, DECIMAL, BOOLEANO, ID;

terminal IMPRIMIR, INT, DOUBLE, BOOL, CHAR, STRINGG, VAR, CONST;

terminal FINCADENA, DOSPUNTOS, PAR1, PAR2;

terminal MAS, MASS, MENOS, UMENOS, MENOSS, DOBLE_ASTERISCO, BARRA, ASTERISCO, PORCENTUAL;

terminal IGUAL, MENOR, MAYOR, NEGACION;
terminal IGUAL_IGUAL, DESIGUAL, MENOR_Q, MAYOR_Q, MENOR_I_Q, MAYOR_I_Q;

terminal PIPE, AMPERSAND, CIRCUNFLEJO;
terminal OR, AND, XOR, NOT;


//no terminales
nonterminal LinkedList<Instruccion> INICIO, INSTRUCCIONES;
nonterminal Instruccion INSTRUCCION;
nonterminal Instruccion IMPRESION, DECLARACION, ASIGNACION, INCREMENTO, DECREMENTO;
nonterminal Tipo TIPOS;
nonterminal Instruccion EXPRESION;

precedence left OR;
precedence left AND;
precedence left XOR;
precedence right NOT;
precedence left IGUAL_IGUAL, DESIGUAL, MENOR_Q, MENOR_I_Q, MAYOR_Q, MAYOR_I_Q;
precedence left MASS, MENOSS;
precedence left ASTERISCO, BARRA, PORCENTUAL;
precedence nonassoc DOBLE_ASTERISCO;
precedence right UMENOS;



start with INICIO;

INICIO ::= INSTRUCCIONES:a      {:   RESULT = a;   :}
;

INSTRUCCIONES ::= INSTRUCCIONES:a INSTRUCCION:b     {: RESULT = a; RESULT.add(b); :}
                | INSTRUCCION:a                     {: RESULT = new LinkedList<>(); RESULT.add(a); :}
;

INSTRUCCION ::= IMPRESION:a         {:   RESULT=a;   :}
                | ASIGNACION:a      {:   RESULT=a;   :}
                | DECLARACION:a     {:   RESULT=a;   :}
                | INCREMENTO:a      {:   RESULT=a;   :}
                | DECREMENTO:a      {:   RESULT=a;   :}
;


IMPRESION ::= IMPRIMIR PAR1 EXPRESION:a PAR2 FINCADENA      {:   RESULT = new Print(a, aleft, aright);   :}
;

ASIGNACION ::= ID:a IGUAL EXPRESION:b FINCADENA  {:   RESULT=new AsignacionVC(a, b, aleft, aright);  :}
;

DECLARACION ::= VAR ID:a DOSPUNTOS TIPOS:b FINCADENA                {:   RESULT = new Declaracion(true, a, b, aleft, aright);       :}
                |VAR ID:a DOSPUNTOS TIPOS:b EXPRESION:c FINCADENA   {:   RESULT = new Declaracion(true, a, b, c , aleft, aright);   :}
                |CONST ID:a DOSPUNTOS TIPOS:b FINCADENA             {:   RESULT = new Declaracion(false, a, b, aleft, aright);      :}
                |CONST ID:a DOSPUNTOS TIPOS:b EXPRESION:c FINCADENA {:   RESULT = new Declaracion(false, a, b, c , aleft, aright);  :}
;

INCREMENTO ::= ID:a MAS MAS FINCADENA                               {:   RESULT = new Incremento(a, aleft, aright);  :}
;

DECREMENTO ::= ID:a MENOS MENOS FINCADENA                           {:   RESULT = new Decremento(a, aleft, aright);  :}
;

TIPOS ::=    INT        {:   RESULT=new Tipo(tipoDato.ENTERO);   :}                                       
            |DOUBLE     {:   RESULT=new Tipo(tipoDato.DECIMAL);   :}  
            |BOOL       {:   RESULT=new Tipo(tipoDato.BOOLEANO);   :}  
            |CHAR       {:   RESULT=new Tipo(tipoDato.CARACTER);   :}  
            |STRINGG    {:   RESULT=new Tipo(tipoDato.CADENA);   :}  
;

EXPRESION ::= MENOS EXPRESION:a                         {: RESULT = new Aritmeticas(a, OperadoresAritmeticos.NEGACION, aleft, aright); :} %prec UMENOS
            | EXPRESION:a DOBLE_ASTERISCO EXPRESION:b   {: RESULT = new Aritmeticas(a, b, OperadoresAritmeticos.POTENCIA, aleft, aright); :} %prec DOBLE_ASTERISCO
            | EXPRESION:a PORCENTUAL EXPRESION:b        {: RESULT = new Aritmeticas(a, b, OperadoresAritmeticos.MODULO, aleft, aright); :} %prec PORCENTUAL
            | EXPRESION:a ASTERISCO EXPRESION:b         {: RESULT = new Aritmeticas(a, b, OperadoresAritmeticos.MULTIPLICACION, aleft, aright); :} %prec ASTERISCO
            | EXPRESION:a BARRA EXPRESION:b             {: RESULT = new Aritmeticas(a, b, OperadoresAritmeticos.DIVISION, aleft, aright); :} %prec BARRA
            | EXPRESION:a MAS EXPRESION:b               {: RESULT = new Aritmeticas(a, b, OperadoresAritmeticos.SUMA, aleft, aright); :} %prec MASS
            | EXPRESION:a MENOS EXPRESION:b             {: RESULT = new Aritmeticas(a, b, OperadoresAritmeticos.RESTA, aleft, aright); :} %prec MENOSS
            
            | EXPRESION:a IGUAL IGUAL EXPRESION:b       {: RESULT = new Relacionales(a, b, OperadoresRelacionales.IGUAL, aleft, aright); :} %prec IGUAL_IGUAL
            | EXPRESION:a NEGACION IGUAL EXPRESION:b    {: RESULT = new Relacionales(a, b, OperadoresRelacionales.NOIGUAL, aleft, aright); :} %prec DESIGUAL
            | EXPRESION:a MENOR EXPRESION:b             {: RESULT = new Relacionales(a, b, OperadoresRelacionales.MENOR, aleft, aright); :} %prec MENOR_Q
            | EXPRESION:a MENOR IGUAL EXPRESION:b       {: RESULT = new Relacionales(a, b, OperadoresRelacionales.IGUAL_MENOR, aleft, aright); :} %prec MENOR_I_Q
            | EXPRESION:a MAYOR EXPRESION:b             {: RESULT = new Relacionales(a, b, OperadoresRelacionales.MAYOR, aleft, aright); :} %prec MAYOR_Q
            | EXPRESION:a MAYOR IGUAL EXPRESION:b       {: RESULT = new Relacionales(a, b, OperadoresRelacionales.IGUAL_MAYOR, aleft, aright); :} %prec MAYOR_I_Q

            | NEGACION EXPRESION:a                      {: RESULT = new Logicos(a, OperadoresLogicos.NOT, aleft, aright); :} %prec NOT
            |EXPRESION:a AMPERSAND AMPERSAND EXPRESION:b{: RESULT = new Logicos(a, b, OperadoresLogicos.AND, aleft, aright); :} %prec AND
            | EXPRESION:a CIRCUNFLEJO EXPRESION:b       {: RESULT = new Logicos(a, b, OperadoresLogicos.XOR, aleft, aright); :} %prec XOR
            | EXPRESION:a PIPE PIPE EXPRESION:b         {: RESULT = new Logicos(a, b, OperadoresLogicos.OR, aleft, aright); :} %prec OR          
            
            | ENTERO:a                                  {: RESULT = new Nativo(Integer.parseInt(a), new Tipo(tipoDato.ENTERO), aleft, aright); :}
            | DECIMAL:a                                 {: RESULT = new Nativo(Double.parseDouble(a), new Tipo(tipoDato.DECIMAL), aleft, aright); :}
            | CADENA:a                                  {: RESULT = new Nativo(a, new Tipo(tipoDato.CADENA), aleft, aright); :}
            | CARACTER:a                                {: RESULT = new Nativo(a, new Tipo(tipoDato.CARACTER), aleft, aright); :}
            | BOOLEANO:a                                {: RESULT = new Nativo(a, new Tipo(tipoDato.BOOLEANO), aleft, aright); :}
            | ID:a                                      {:   RESULT = new AccesoVC(a, aleft, aright);   :}
            | PAR1 EXPRESION:a PAR2                     {:   RESULT = a;    :}
;
